apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        releaseConfig
    }
    compileSdkVersion 23
    buildToolsVersion "21.1.2"
    defaultConfig {
        applicationId "me.rei_m.hbfavmaterial"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 9
        versionName "1.2.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "d"
            ext.enableCrashlytics = false
        }
        release {
            signingConfig signingConfigs.releaseConfig
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    if (project.hasProperty('storeFile')) {
        android.signingConfigs.releaseConfig.storeFile = file(storeFile)
    }
    if (project.hasProperty('storePassword')) {
        android.signingConfigs.releaseConfig.storePassword = storePassword
    }
    if (project.hasProperty('keyAlias')) {
        android.signingConfigs.releaseConfig.keyAlias = keyAlias
    }
    if (project.hasProperty('keyPassword')) {
        android.signingConfigs.releaseConfig.keyPassword = keyPassword
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    dataBinding {
        enabled = false
    }
    testBuildType "debug"
}

kapt {
    generateStubs = true
}

dependencies {
    ext.android_support_lib_version = '23.2.1'
    ext.rx_android_kotlin_version = '0.4.0'
    ext.kotlinx_dom_version = '0.0.10'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.jakewharton.rxbinding:rxbinding-kotlin:$rx_android_kotlin_version"
    compile "com.jakewharton.rxbinding:rxbinding-support-v4-kotlin:$rx_android_kotlin_version"
    compile "com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:$rx_android_kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx.dom:$kotlinx_dom_version"

    compile 'com.google.dagger:dagger:2.1'
    kapt 'com.google.dagger:dagger-compiler:2.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    kapt 'com.android.databinding:compiler:1.0-rc5'

    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile "com.android.support:support-annotations:$android_support_lib_version"
    compile "com.android.support:appcompat-v7:$android_support_lib_version"
    compile "com.android.support:design:$android_support_lib_version"
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.picasso:picasso:2.5.2'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    compile 'org.jsoup:jsoup:1.8.3'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'io.reactivex:rxjava:1.1.1'
    compile "com.android.support:support-v4:$android_support_lib_version"
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile('com.twitter.sdk.android:twitter:1.13.0@aar') {
        transitive = true;
    }
}
buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlinx.dom"
    }
    maven {
        url 'https://maven.fabric.io/public'
    }
}
